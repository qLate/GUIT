# wayland-client
find_path(
  WAYLAND_CLIENT_INCLUDE_DIR
  NAMES wayland-client.h
)

find_library(
  WAYLAND_CLIENT_LIBRARY
  NAMES wayland-client libwayland-client
)

if(WAYLAND_CLIENT_INCLUDE_DIR AND WAYLAND_CLIENT_LIBRARY)
  add_library(wayland::client UNKNOWN IMPORTED)

  set_target_properties(
    wayland::client PROPERTIES
    INTERFACE_INCLUDE_DIRECTORIES "${WAYLAND_CLIENT_INCLUDE_DIR}"
    IMPORTED_LINK_INTERFACE_LANGUAGES "C"
    IMPORTED_LOCATION "${WAYLAND_CLIENT_LIBRARY}"
  )
endif()

# wayland-server
find_path(
  WAYLAND_SERVER_INCLUDE_DIR
  NAMES wayland-server.h
)

find_library(
  WAYLAND_SERVER_LIBRARY
  NAMES wayland-server libwayland-server
)

if(WAYLAND_SERVER_INCLUDE_DIR AND WAYLAND_SERVER_LIBRARY)
  add_library(wayland::server UNKNOWN IMPORTED)

  set_target_properties(
    wayland::server PROPERTIES
    INTERFACE_INCLUDE_DIRECTORIES "${WAYLAND_SERVER_INCLUDE_DIR}"
    IMPORTED_LINK_INTERFACE_LANGUAGES "C"
    IMPORTED_LOCATION "${WAYLAND_SERVER_LIBRARY}"
  )
endif()


# wayland-cursor
find_path(
  WAYLAND_CURSOR_INCLUDE_DIR
  NAMES wayland-cursor.h
)

find_library(
  WAYLAND_CURSOR_LIBRARY
  NAMES wayland-cursor libwayland-cursor
)

if(WAYLAND_CURSOR_INCLUDE_DIR AND WAYLAND_CURSOR_LIBRARY)
  add_library(wayland::cursor UNKNOWN IMPORTED)

  set_target_properties(
    wayland::cursor PROPERTIES
    INTERFACE_INCLUDE_DIRECTORIES "${WAYLAND_CURSOR_INCLUDE_DIR}"
    IMPORTED_LINK_INTERFACE_LANGUAGES "C"
    IMPORTED_LOCATION "${WAYLAND_CURSOR_LIBRARY}"
  )
endif()


include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(
  WAYLAND_CLIENT
  REQUIRED_VARS WAYLAND_CLIENT_LIBRARY WAYLAND_CLIENT_INCLUDE_DIR
)

find_package_handle_standard_args(
  WAYLAND_SERVER
  REQUIRED_VARS WAYLAND_SERVER_LIBRARY WAYLAND_SERVER_INCLUDE_DIR
)

find_package_handle_standard_args(
  WAYLAND_CURSOR
  REQUIRED_VARS WAYLAND_CURSOR_LIBRARY WAYLAND_CURSOR_INCLUDE_DIR
)


mark_as_advanced(
  WAYLAND_CLIENT_INCLUDE_DIR
  WAYLAND_CLIENT_LIBRARY
  WAYLAND_SERVER_INCLUDE_DIR
  WAYLAND_SERVER_LIBRARY
  WAYLAND_CURSOR_INCLUDE_DIR
  WAYLAND_CURSOR_LIBRARY
)